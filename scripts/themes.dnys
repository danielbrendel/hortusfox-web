# Theme management script
#
# Requires AquaShell (https://github.com/danielbrendel/dnyAquaShell) as scripting shell.
#
# This script can be launched via the following arguments
#  * --list (Lists all installed themes)
#  * --install "name"
#  * --remove "name"
# You can run the script from the project's root directory as follows:
# > aquashell scripts/themes.dnys [arguments]
#

require "fileio";

global szCurrentScriptPath string;
global bArgumentExists bool;
global szArgumentValue string;

getscriptpath szCurrentScriptPath;

exec "%szCurrentScriptPath/scripts/tmp/urls.conf.dnys";

function ListInstalledThemes int(baseObject string, iteratedObject string)
{
    local dirtype bool;
    
    fdisdir "%baseObject%iteratedObject" dirtype;
    if (%dirtype, -eq, true) {
        print "- %iteratedObject";
    };

    result 1;
};

function list_themes void()
{
    local enumres bool;
    
    print "Installed themes:";
    denum {%szCurrentScriptPath/public/themes/} "*.*" "ListInstalledThemes" enumres;

    print "";
};

function install_theme void(themeName string)
{
    local checkdir bool;
    local checkrem bool;

    print "Attempting to install %themeName...";

    dexists "%szCurrentScriptPath/public/themes/%themeName" checkdir;
    if (%checkdir, -nt, true) {
        sys {curl %APP_SERVICE_URL/downloads/%themeName.zip --output %szCurrentScriptPath/public/themes/%themeName.zip --silent};
        sys {powershell -Command "Expand-Archive %szCurrentScriptPath/public/themes/%themeName.zip -DestinationPath %szCurrentScriptPath/public/themes"};

        fremove "%szCurrentScriptPath/public/themes/%themeName.zip" checkrem;

        dexists "%szCurrentScriptPath/public/themes/%themeName" checkdir;
        if (%checkdir, -eq, true) {
            print "Done.";
        } else {
            print "Error: installation failed.";
        };
    } else {
        print "Theme %themeName is already installed.";
    };
};

function remove_theme void(themeName string)
{
    local checkdir bool;

    print "Removing %themeName...";

    dexists "%szCurrentScriptPath/public/themes/%themeName" checkdir;
    if (%checkdir, -eq, true) {
        sys {rmdir /S /Q "%szCurrentScriptPath/public/themes/%themeName"};

        dexists "%szCurrentScriptPath/public/themes/%themeName" checkdir;
        if (%checkdir, -nt, true) {
            print "Done.";
        } else {
            print "Error: removal failed.";
        };
    } else {
        print "Theme %themeName not found.";
    };
};

exec "%szCurrentScriptPath/scripts/incl/args.dnys";

call CmdHas("--list") => bArgumentExists;
if (%bArgumentExists, -eq, true) {
	call list_themes() => void;
    exit;
};

call CmdPair("--install") => szArgumentValue;
if (%szArgumentValue, -nt, "") {
	call install_theme("%szArgumentValue") => void;
    exit;
};

call CmdPair("--remove") => szArgumentValue;
if (%szArgumentValue, -nt, "") {
	call remove_theme("%szArgumentValue") => void;
    exit;
};

print "No valid argument provided. Use --list, --install [name] or --remove [name]";
