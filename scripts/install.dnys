# HortusFox install script
#
# This script installs HortusFox on the current system
# 
# Requires AquaShell (https://github.com/danielbrendel/dnyAquaShell) as scripting shell.
# It's mandatory that PHP, MariaDB and Composer are installed and available via the commandline.
# Also your web server and mariadb server must be launched before running this script.
#
# This script can be launched in interactive mode, or with the following arguments
#  * --workspace "Your workspace name"
#  * --username "Your admin profile name"
#  * --email "your@loginmail.tld"
#  * --password "your-login-password"
#  * --database "hortusfox"
#  * --dbhost "localhost"
#  * --dbport "3306"
#  * --dbuser "root"
#  * --dbpw "your-access-password"
#  * --continue (skips the confirmation prompt)
#  * --launch (opens http://localhost in your default browser)
# You can run the script from the project's root directory as follows:
# > aquashell scripts/install.dnys [opt:arguments]
#

require "auto";
require "dialog";
require "fileio";
require "strings";

const C_GITHUB_URL string <= "https://github.com/danielbrendel/hortusfox-web";
const C_SERVICE_URL string <= "https://www.hortusfox.com";

global szCurrentScriptPath string;
global szInputWorkspaceName string;
global szInputUserName string;
global szInputUserEmail string;
global szInputUserPassword string;
global szInputDbName string;
global szInputDbHost string;
global szInputDbPort string;
global szInputDbUser string;
global szInputDbPw string;
global szInputContinue string;
global szArgumentValue string;
global bArgumentExists bool;

function ValidateIndicatorFile void()
{
	local bIndicatorFileExists bool;
	
	fexists "%szCurrentScriptPath/do_install" bIndicatorFileExists;
	if (%bIndicatorFileExists, -eq, false) {
		print "Indicator file not found! Aborting...";
		exit;
	};
};

function StepComposer void()
{
	sys {composer install --ignore-platform-reqs --working-dir "%szCurrentScriptPath"};
};

function StepEnvironmentConfig void()
{
	local hEnvFile int;
	local bFileOpened bool;

	fopen "%szCurrentScriptPath/.env" false hEnvFile;
	fisopen %hEnvFile bFileOpened;
	if (%bFileOpened, -eq, true) {
		fwriteline %hEnvFile "# App settings";
		fwriteline %hEnvFile {APP_NAME="HortusFox"};
		fwriteline %hEnvFile {APP_VERSION="5.6"};
		fwriteline %hEnvFile {APP_AUTHOR="Daniel Brendel"};
		fwriteline %hEnvFile {APP_CONTACT="dbrendel1988@gmail.com"};
		fwriteline %hEnvFile {APP_DEBUG=true};
		fwriteline %hEnvFile {APP_BASEDIR=""};
		fwriteline %hEnvFile {APP_LANG="en"};
		fwriteline %hEnvFile {APP_WORKSPACE="%szInputWorkspaceName"};
		fwriteline %hEnvFile {APP_OVERDUETASK_HOURS=10};
		fwriteline %hEnvFile {APP_CRONJOB_MAILLIMIT=5};
		fwriteline %hEnvFile {APP_GITHUB_URL="%C_GITHUB_URL"};
		fwriteline %hEnvFile {APP_SERVICE_URL="%C_SERVICE_URL"};
		fwriteline %hEnvFile "";
		fwriteline %hEnvFile "# Session";
		fwriteline %hEnvFile {SESSION_ENABLE=true};
		fwriteline %hEnvFile {SESSION_DURATION=31536000};
		fwriteline %hEnvFile {SESSION_NAME=null};
		fwriteline %hEnvFile "";
		fwriteline %hEnvFile "# Photo resize factors";
		fwriteline %hEnvFile {PHOTO_RESIZE_FACTOR_DEFAULT=1.0};
		fwriteline %hEnvFile {PHOTO_RESIZE_FACTOR_1=0.5};
		fwriteline %hEnvFile {PHOTO_RESIZE_FACTOR_2=0.4};
		fwriteline %hEnvFile {PHOTO_RESIZE_FACTOR_3=0.4};
		fwriteline %hEnvFile {PHOTO_RESIZE_FACTOR_4=0.3};
		fwriteline %hEnvFile {PHOTO_RESIZE_FACTOR_5=0.2};
		fwriteline %hEnvFile "";
		fwriteline %hEnvFile "# Database settings";
		fwriteline %hEnvFile {DB_ENABLE=true};
		fwriteline %hEnvFile {DB_HOST="%szInputDbHost"};
		fwriteline %hEnvFile {DB_USER="%szInputDbUser"};
		fwriteline %hEnvFile {DB_PASSWORD="%szInputDbPw"};
		fwriteline %hEnvFile {DB_PORT=%szInputDbPort};
		fwriteline %hEnvFile {DB_DATABASE="%szInputDbName"};
		fwriteline %hEnvFile {DB_DRIVER=mysql};
		fwriteline %hEnvFile {DB_CHARSET="utf8mb4"};
		fwriteline %hEnvFile "";
		fwriteline %hEnvFile "# SMTP settings";
		fwriteline %hEnvFile {SMTP_AUTH=true};
		fwriteline %hEnvFile {SMTP_FROMNAME=""};
		fwriteline %hEnvFile {SMTP_FROMADDRESS=""};
		fwriteline %hEnvFile {SMTP_HOST=""};
		fwriteline %hEnvFile {SMTP_PORT=587};
		fwriteline %hEnvFile {SMTP_USERNAME=""};
		fwriteline %hEnvFile {SMTP_PASSWORD=""};
		fwriteline %hEnvFile {SMTP_ENCRYPTION=tls};
		fwriteline %hEnvFile "";
		fwriteline %hEnvFile "# Logging";
		fwriteline %hEnvFile {LOG_ENABLE=true};
		fclose %hEnvFile;
	} else {
		print "Error: Could not create .env";
		exit;
	};
};

function StepDatabaseCreation void()
{
	sys {mysql -u %szInputDbUser -e "CREATE DATABASE IF NOT EXISTS `%szInputDbName`"};
};

function StepSeedingTables void()
{
	sys {php asatru migrate:fresh};
	sys {php asatru calendar:classes};
	sys {php asatru plants:attributes};
};

function StepCreateAppConfig void()
{
	sys {mysql -u %szInputDbUser -e "INSERT INTO `%szInputDbName`.`AppModel` (workspace) VALUES('%szInputWorkspaceName')"};
};

function StepAddAdminUser void()
{
	local szHashedPassword string;
	
	sys {php -r "echo password_hash('%szInputUserPassword', PASSWORD_BCRYPT);"} szHashedPassword;

	sys {mysql -u %szInputDbUser -e "INSERT INTO `%szInputDbName`.`UserModel` (name, email, password, admin) VALUES('%szInputUserName', '%szInputUserEmail', '%szHashedPassword', 1)"};
};

function CheckLaunch void()
{
	local bCmdHasResult bool;

	call CmdHas("--launch") => bCmdHasResult;
	if (%bCmdHasResult, -eq, true) {
		aut_run "http://localhost" "" "" void;
	};
};

function CleanUp void()
{
	local bRemovalOperationStatus bool;
	
	fremove "%szCurrentScriptPath/do_install" bRemovalOperationStatus;
	if (%bRemovalOperationStatus, -nt, true) {
		print "Warning: Could not remove indicator file";
	};
};

getscriptpath szCurrentScriptPath;

exec "%szCurrentScriptPath/scripts/incl/args.dnys";

call ValidateIndicatorFile();

print "Welcome to the HortusFox commandline installation%CR%LF";

call CmdPair("--workspace") => szArgumentValue;
if (%szArgumentValue, -nt, "") {
	set szInputWorkspaceName <= "%szArgumentValue";
} else {
	input szInputWorkspaceName "Enter workspace name: ";
};

call CmdPair("--username") => szArgumentValue;
if (%szArgumentValue, -nt, "") {
	set szInputUserName <= "%szArgumentValue";
} else {
	input szInputUserName "Enter your name: ";
};

call CmdPair("--email") => szArgumentValue;
if (%szArgumentValue, -nt, "") {
	set szInputUserEmail <= "%szArgumentValue";
} else {
	input szInputUserEmail "Enter your e-mail: ";
};

call CmdPair("--password") => szArgumentValue;
if (%szArgumentValue, -nt, "") {
	set szInputUserPassword <= "%szArgumentValue";
} else {
	input szInputUserPassword "Enter login password: ";
};

call CmdPair("--database") => szArgumentValue;
if (%szArgumentValue, -nt, "") {
	set szInputDbName <= "%szArgumentValue";
} else {
	input szInputDbName "Enter database name: ";
};

call CmdPair("--dbhost") => szArgumentValue;
if (%szArgumentValue, -nt, "") {
	set szInputDbHost <= "%szArgumentValue";
} else {
	input szInputDbHost "Enter database host: ";
};

call CmdPair("--dbport") => szArgumentValue;
if (%szArgumentValue, -nt, "") {
	set szInputDbPort <= "%szArgumentValue";
} else {
	input szInputDbPort "Enter database port: ";
};

call CmdPair("--dbuser") => szArgumentValue;
if (%szArgumentValue, -nt, "") {
	set szInputDbUser <= "%szArgumentValue";
} else {
	input szInputDbUser "Enter database user: ";
};

call CmdPair("--dbpw") => szArgumentValue;
if (%szArgumentValue, -nt, "") {
	if (%szArgumentValue, -eq, "!") {
		set szArgumentValue <= "";
	};

	set szInputDbPw <= "%szArgumentValue";
} else {
	input szInputDbPw "Enter database password: ";
};

call CmdHas("--continue") => bArgumentExists;
if (%bArgumentExists, -nt, true) {
	input szInputContinue "All set. Do you want to continue? (y/n) ";

	s_lower szInputContinue;
	
	if ("%szInputContinue", -eq, "n") {
		print "Aborted by user";
		exit;
	};
};

print "Proceeding with installation...%CR%LF";

print "> Installing composer";
call StepComposer();

print "> Creating environment config";
call StepEnvironmentConfig();

print "> Creating database";
call StepDatabaseCreation();

print "> Seeding tables";
call StepSeedingTables();

print "> Creating app config";
call StepCreateAppConfig();

print "> Adding admin user";
call StepAddAdminUser();

print "%CR%LF>> Installation done <<!%CR%LF";

call CheckLaunch();
call CleanUp();
